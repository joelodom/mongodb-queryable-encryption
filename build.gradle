plugins {
    id 'java'
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.github.cdimascio:java-dotenv:5.2.2'
    implementation 'org.mongodb:mongodb-driver-sync:5.3.0'
    implementation 'org.mongodb:mongodb-crypt:5.3.0'
}

application {
    mainClass = 'com.joelodom.QEDemonstration'
}

run {
    standardInput = System.in
}

// See the README about building a JAR file with the crypt_shared dependency
// built in.

tasks.register('fatJar', Jar) {
    manifest {
        attributes 'Main-Class': application.mainClass // Defines the main class
    }
    
    // Include compiled classes
    from sourceSets.main.output

    // Include all dependencies by extracting them inside the JAR
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

   from('lib') {
    into('lib')
   }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE // Ignore duplicate files
}
